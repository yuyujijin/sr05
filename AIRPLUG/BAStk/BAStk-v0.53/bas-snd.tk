#    bas
#    an airplug compatible program
#    author: Bertrand Ducourthial AT utc.fr, Anthony Buisset (v0.1)
#    license type: free of charge license for academic and research purpose
#    see license.txt
### SENDING MODULE #############################################################

APG_vrb_dispnotif "  Loading bas-snd.tk (sending module)" "bas-snd" 3

### 1. MODULE VARIABLES ########################################################
# Message par defaut pour l'emission
set BAS_snd_msg $BAS_rc_msgdefault

# Application par defaut vers qui sera emis le message
set BAS_snd_app $BAS_rc_appdefault

# Delai par defaut entre deux messages.
# NB : peut etre modifie par option --delay=
set BAS_snd_delay $BAS_rc_delaydefault

# Demande de depart automatique des emissions periodiques
# NB : peut etre change par l'option --autosend
set BAS_snd_autosend false

# Etat d'emission periodique en cours ou non.
set BAS_snd_sending false

# Numero du message emis
set BAS_snd_nseq 0


### 2. MODULE GUI ##############################################################
labelframe .bassnd -pady 2 -padx 2 -text "Emission \[$APP, ident = $APG_ident\]" -fg $APG_int_coltitle

button .bassnd.btauto -text "Auto" \
		-activebackground $APG_int_colbutton \
		-foreground $APG_int_colbutton \
		-font $APG_int_fnbutton \
		-width 10 \
		-state disable \
		-command BAS_snd_btauto


labelframe .bassnd.app -text "app"
entry .bassnd.app.v -width 4 -textvariable BAS_snd_app

labelframe .bassnd.del -text "d√©lai (ms)"
set value $BAS_snd_delay
spinbox .bassnd.del.v -width 6 -textvariable BAS_snd_delay -from 100 -to 10000 -increment 100 -justify right
.bassnd.del.v set $value

labelframe .bassnd.msg -text "message"
entry .bassnd.msg.v -width 64 -textvariable BAS_snd_msg

labelframe .bassnd.nseq -text "nseq"
label .bassnd.nseq.v -width 6 -textvariable BAS_snd_nseq -anchor e

# affichage des sous-zones de la zone snd
pack .bassnd.btauto .bassnd.del .bassnd.app .bassnd.msg .bassnd.app .bassnd.nseq -side left -fill y -pady 2
pack .bassnd.msg.v .bassnd.app.v .bassnd.del.v .bassnd.nseq.v


### 3. DECODING COMMAND LINE OPTIONS OF THE MODULE #############################
foreach option $APG_lstargs {
    set name [lindex [split $option "="] 0]
    set value [lindex [split $option "="] 1]
    
    switch -- $name {
				--bas-dest {
						# application destinataire -----------------------------------------
						if { $value == "" } {
								APG_vrb_dispwarning "    Option $name trouvee sans argument, utilisation de la valeur par defaut : $BAS_snd_app"
						} else {
								set BAS_snd_app $value
								APG_vrb_dispnotif "    Option $option trouvee, nouvelle application : $BAS_snd_app"
						}
				}
				
				--bas-delay {
						# periode d'emission -----------------------------------------------
						if { $value == "" } {
								APG_vrb_dispwarning "    Option $name trouvee sans argument, utilisation de la valeur par defaut : $BAS_snd_delay"
						} else {
								set BAS_snd_delay $value
								APG_vrb_dispnotif "    Option $option trouvee, nouvelle periode : $BAS_snd_delay"
						}
				}

				--bas-autosend {
						# emission automatique ---------------------------------------------
						set BAS_snd_autosend true
						APG_vrb_dispnotif "    Option $option trouvee, mode emission automatique active"
				}
    }
}



### 4. PROCEDURES OF THE GUI OF THE MODULE #####################################
proc BAS_snd_btauto { } {

		if { $::BAS_snd_sending == false } {
				.bassnd.btauto configure -text "Stop"
				set ::BAS_snd_sending true
				BAS_snd_autosend
		} else {
				after cancel BAS_snd_autosend
				set ::BAS_snd_sending false
				.bassnd.btauto configure -text "Auto"
		}
}



### 5. PROCEDURES OF THE MODULE ################################################

#-- Procedure BAS_snd_send ----------------------------------------------------#
# Action : envoie periodiquement le message sur le reseau                      #
# Entree : rien                                                                #
# Retour : rien                                                                #
#------------------------------------------------------------------------------#
proc BAS_snd_autosend { } {
    
		# Incrementation du numero de message
		incr ::BAS_snd_nseq

    # Creation du message formate
    set msg [APG_msg_createmsg $::APG_bas_mnemopayload $::BAS_snd_msg]

		# Ajout du numero de sequence
		APG_msg_addmsg msg $::APG_bas_mnemonseq $::BAS_snd_nseq

    APG_send_whatwho $msg $::BAS_snd_app
		
		after $::BAS_snd_delay BAS_snd_autosend
}

